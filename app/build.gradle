apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "de.blocbox.simpleaccount"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName '0.1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    buildFeatures {
        dataBinding false
    }
    buildTypes {
        release {
            debuggable false
        }
        applicationVariants.all{
            variant ->
                variant.outputs.each{
                    output->
                        project.ext { appName = 'SimpleAccount' }
                        def newName = output.outputFile.name
                        newName = newName.replace("app-", "$project.ext.appName-")
                        newName = newName.replace("-release", "")
                        output.outputFileName  = newName
                }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'
}
